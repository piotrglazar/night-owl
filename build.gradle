buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.2.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

repositories {
    mavenCentral()
}

dependencies {
    // gui
    compile files("./external/intellij_forms_rt.jar")
    // guava
    compile("com.google.guava:guava:19.0")
    // context
    compile("org.springframework:spring-context:4.3.2.RELEASE")
    // context-support
    compile("org.springframework:spring-context-support:4.3.2.RELEASE")
    // tag
    compile("org.slf4j:slf4j-api:1.7.12")
    compile("ch.qos.logback:logback-classic:1.1.3")
    // db
    compile("org.hibernate:hibernate-entitymanager:4.3.11.Final")
    compile("org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final")
    compile("org.springframework.data:spring-data-jpa:1.10.2.RELEASE")
    compile("org.hsqldb:hsqldb:2.3.3")
    // annotations
    compile("com.google.code.findbugs:jsr305:3.0.0")
    compile("com.nesscomputing.testing:findbugs-annotations:2.0.0")

    testCompile("org.springframework:spring-test:4.3.2.RELEASE")
    testCompile("junit:junit:4.12")
    testCompile("org.mockito:mockito-all:2.0.2-beta")
    testCompile("org.assertj:assertj-core:3.1.0")
    testCompile("eu.codearte.catch-exception:catch-exception:1.4.4")
    testCompile("nl.jqno.equalsverifier:equalsverifier:1.7.3")
    testCompile("pl.pragmatists:JUnitParams:1.0.4")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

jacoco {
    toolVersion = '0.7.6.+'
}

jacocoTestReport {
    reports {
        html.destination "$buildDir/reports/jacoco"
        xml.enabled = true
        html.enabled = true
    }
}


sourceCompatibility = 1.8

mainClassName = "com.piotrglazar.nightowl.NightOwl"

checkstyle {
    toolVersion = '6.7'
    ignoreFailures = true
}

pmd {
    ignoreFailures = true
}

findbugs {
    toolVersion = '3.0.0'
    effort = 'max'
    ignoreFailures = true
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    exclude '**/com/piotrglazar/nightowl/ui/DefaultMainWindow*'
    classes = classes.filter {
        !it.path.contains(new File('com/piotrglazar/nightowl/ui/DefaultMainWindow.class').path)
    }
}

tasks.withType(Pmd) {
    exclude '**/com/piotrglazar/nightowl/util/DoubleConverter*'
}

scmVersion {
    tag {
        prefix = 'night-owl'
    }
}

// make our version available to all
project.version = scmVersion.version

jar {
    baseName = 'night-owl'
    version =  project.version
    manifest {
        attributes 'Main-Class': 'com.piotrglazar.nightowl.NightOwl'
    }
}
